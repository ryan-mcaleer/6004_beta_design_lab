
.subckt regfile clk werf ra2sel ra[4:0] rb[4:0] rc[4:0]
+ wdata[31:0] radata[31:0] rbdata[31:0]

    * RA2SEL mux: 32-bit version using 1-bit mux2
    Xmux ra2sel#5 rb[4:0] rc[4:0] ra2_input[4:0] mux2

    * check for r31 read / writes
    Xchecka ra[4:0] is_ra1_input_31 checkreg

    * check output of ra2sel mux, rather than b explicitly
    Xcheckb ra2_input[4:0] is_ra2_input_31 checkreg

    Xcheckc rc[4:0] is_wa_input_31 checkreg

    Xregfile
    + vdd 0 0 ra[4:0] mem_radata[31:0] // A read port
    + vdd 0 0 ra2_input[4:0] mem_rbdata[31:0] // B read port
    + 0 clk werf rc[4:0] wdata[31:0] // write port
    + $memory width=32 nlocations=31

    * Invert R31 detect signals
    XinvA is_ra1_input_31 not_ra1_31 inverter
    XinvB is_ra2_input_31 not_ra2_31 inverter

    * Port A read data gating (force R31 to zero)
    XandA mem_radata[31:0] not_ra1_31#32 radata[31:0] and2


    * Port B read data gating (force R31 to zero)
    XandB mem_rbdata[31:0] not_ra2_31#32 rbdata[31:0] and2
.ends


.subckt checkreg addr[4:0] is_r31

    Xand0 addr[4] addr[3] addr[2] addr_hi and3
    Xand1 addr[1] addr[0] addr_lo and2
    Xand2 addr_hi addr_lo is_r31 and2

.ends